plugins {
    id 'java'
    id 'war'
    id "fish.payara.micro-gradle-plugin" version "1.1.0"
    id 'com.diffplug.spotless' version '6.23.3'
}

group = 'io.ceze'
version = '0.0.1'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

dependencies {
    providedCompile 'jakarta.platform:jakarta.jakartaee-web-api:10.0.0'
    providedCompile 'org.eclipse.microprofile:microprofile:6.1'
    implementation(platform('fish.payara.api:payara-bom:6.2024.2'))
    providedCompile 'fish.payara.api:payara-api:6.12.0'

    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.google.maps:google-maps-services:2.2.0'


    testImplementation(platform('org.junit:junit-bom:5.10.1'))
    testImplementation('org.junit.jupiter:junit-jupiter') {
        exclude group: 'org.junit-vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.testcontainers:testcontainers:1.19.3'
    testImplementation 'org.testcontainers:postgresql:1.19.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
}

repositories {
    maven {
        url 'https://nexus.dev.payara.fish/repository/payara-artifacts'
        name 'payara-nexus-artifacts'
    }
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

payaraMicro {
    payaraVersion = '6.2024.2'
    contextRoot = '/'
    commandLineOptions = [nocluster: '', nohazelcast: '', autobindhttp: '']
}


spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.17.0')
        licenseHeaderFile "${rootDir}/gradle/spotless.java.license.txt"
        //        ratchetFrom 'origin/master'
        trimTrailingWhitespace()
        endWithNewline()
        targetExclude '**/Tmp*.java'
        indentWithSpaces(4)
        removeUnusedImports()
        importOrder("java.*", "jakarta.*", "fish.payara.*", "io.ceze.*")
    }
    groovyGradle {
        target '**/*.gradle'
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

war {
    archiveFileName = 'regulus.war'
}
