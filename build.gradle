plugins {
    id 'java'
    id 'war'
    id "fish.payara.micro-gradle-plugin" version "1.1.0"
    id 'com.palantir.git-version' version '0.15.0'
    id 'com.diffplug.spotless' version '6.23.3'
}

group = 'io.ceze'
version = gitVersion()

java {
    sourceCompatibility = '21'
}

dependencies {
    providedCompile 'jakarta.platform:jakarta.jakartaee-web-api:10.0.0'
    providedCompile 'org.eclipse.microprofile:microprofile:6.1'
    implementation(platform('fish.payara.api:payara-bom:6.2024.2'))
    providedCompile 'fish.payara.api:payara-api:6.12.0'

    //    implementation 'org.hibernate.orm:hibernate-core:6.4.1.Final'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    //    runtimeOnly 'org.postgresql:postgresql:42.6.0'

    testImplementation(platform('org.jboss.arquillian:arquillian-bom:1.8.0.Final'))
    testImplementation 'org.jboss.arquillian.junit5:arquillian-junit5-container'
    testImplementation 'fish.payara.arquillian:arquillian-payara-server-embedded:3.0.alpha8'
    testImplementation 'fish.payara.extras:payara-embedded-all:6.2024.2'
    testImplementation(platform('org.junit:junit-bom:5.10.1'))
    testImplementation('org.junit.jupiter:junit-jupiter') {
        exclude group: 'org.junit-vintage', module: 'junit-vintage-engine'
    }
    //    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    //    testImplementation 'org.testcontainers:postgresql:1.19.3'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
}

repositories {
    maven {
        url 'https://nexus.dev.payara.fish/repository/payara-artifacts'
        name 'payara-nexus-artifacts'
    }
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

payaraMicro {
    payaraVersion = '6.2024.2'
    contextRoot = '/'
    commandLineOptions = [nocluster: '', nohazelcast: '', autobindhttp: '']
}


spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.17.0')
        licenseHeaderFile "${rootDir}/gradle/spotless.java.license.txt"
        //        ratchetFrom 'origin/master'
        trimTrailingWhitespace()
        endWithNewline()
        targetExclude '**/Tmp*.java'
        indentWithSpaces(4)
        removeUnusedImports()
        importOrder("java.*", "jakarta.*", "fish.payara.*", "io.ceze.*")
    }
    groovyGradle {
        target '**/*.gradle'
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}
